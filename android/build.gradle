buildscript {
    // The Android Gradle plugin is only required when opening the android folder stand-alone.
    // This avoids unnecessary downloads and potential conflicts when the library is included as a
    // module dependency in an application project.
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
            maven {
                url 'https://maven.google.com/'
                name 'Google'
            }
        }
        def buildGradleVersion = ext.has('buildGradlePluginVersion') ? ext.get('buildGradlePluginVersion') : '4.2.0'

        dependencies {
            classpath "com.android.tools.build:gradle:$buildGradleVersion"
        }
    }
}

plugins {
    id 'com.android.library'
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def verCode = 1
def verName = "1.0.0"

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 34)

    def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION
    // Check AGP version for backward compatibility reasons
    if (agpVersion.tokenize('.')[0].toInteger() >= 7) {
        namespace = "com.simi.pictureselector"
    }
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion safeExtGet('targetSdkVersion', 34)

        versionCode verCode
        versionName verName

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'  // 确保包含设备所用的 ABI
        }
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17 -frtti -fexceptions"  // 确保支持 RTTI 和异常
            }
        }
    }
    lintOptions {
        warning 'InvalidPackage', 'MissingPermission'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        buildConfig = true
    }
}
repositories {
    mavenCentral()
    google()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$rootDir/../node_modules/react-native/android"
    }
}

dependencies {
    implementation "com.facebook.react:react-native:${safeExtGet('reactNativeVersion', '+')}"
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.activity:activity:1.3.1"
    implementation "androidx.fragment:fragment:1.3.1"
    implementation "androidx.exifinterface:exifinterface:1.1.0"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "com.github.bumptech.glide:glide:4.15.1"
    implementation 'io.github.lucksiege:compress:v3.11.2'
    implementation 'io.github.lucksiege:ucrop:v3.11.2'
}
